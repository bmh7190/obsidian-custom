

---
### 🏛️ Instruction Set

**Instruction Set**은 컴퓨터가 수행할 수 있는 기능들의 집합이다.  
각 컴퓨터마다 고유한 **Instruction Set**을 가지지만, 기본적인 동작 원리는 유사하다.  
초기 컴퓨터들은 매우 단순한 Instruction Set을 가지고 있었으며,  
오늘날의 많은 현대 컴퓨터들도 여전히 간결한 Instruction Set을 유지하고 있다.

---

##  **Arithmetic Operations**

**덧셈(Add)과 뺄셈(Subtract)** 연산은 **3개의 피연산자(Operands)**를 사용한다.

- **두 개(Source)**는 연산할 값이고,
- **하나(Destination)**는 결과를 저장할 레지스터이다.

assembly

복사편집

`add a, b, c  # a ← b + c sub a, b, c  # a ← b - c`

모든 산술 연산(Arithmetic Operations)은 이와 같은 형식을 따른다.

> **📌 Design Principle 1**: 단순한 연산일수록 규칙적인 형식을 따르는 것이 좋다.

---

##  **Register Operands**

산술 연산(Arithmetic Instructions)은 **레지스터(Register)**를 사용하여 데이터를 처리한다.  
MIPS 프로세서는 **32개의 32비트 레지스터 파일(Register File)**을 제공하며,  
이는 **자주 사용되는 데이터**를 저장하는 용도로 활용된다.

- 레지스터는 **0번부터 31번까지 번호가 부여**되며,
- 32비트 데이터 단위를 **워드(Word)**라고 부른다.

💾 **어셈블러(Assembler)에서 사용하는 레지스터 명칭**

- `$t0, $t1, ... , $t9` → **임시 값(Temporary Values) 저장**
- `$s0, $s1, ... , $s7` → **저장된 변수(Saved Variables) 저장**

> **📌 Design Principle 2**: **레지스터 크기가 작을수록 연산 속도는 더 빨라진다.**

---
##  **Main Memory for Composite Data**

산술 연산(Arithmetic Operations)을 적용하기 위해, **메인 메모리(Main Memory)**를 사용하여 데이터를 불러오거나 저장할 수 있다.

#### **📌 메모리에서 데이터 로드 및 저장**

```assembly
lw $t0, 8($s1)  # 메모리에서 값을 불러와 $t0에 저장 (Load Word) 
sw $t0, 8($s1)  # $t0의 값을 메모리에 저장 (Store Word)`
```

- `lw (Load Word)` : 메모리에서 데이터를 불러와 레지스터에 저장
- `sw (Store Word)` : 레지스터 값을 다시 메모리에 저장

---

##  **메모리 주소와 데이터 정렬**

✅ **MIPS의 메모리 특성**

- **메모리는 바이트(Byte) 단위로 주소가 할당됨**
- **각 주소(Address)는 8비트(1바이트) 데이터를 가리킴**
- **워드(Word)는 메모리에 정렬(Aligned)되어 저장됨**
- **주소는 항상 4의 배수(Multiple of 4)여야 함**

✅ **MIPS는 Big Endian 방식을 사용**

- 워드(Word) 내에서 **가장 큰 값(Most Significant Byte, MSB)**이 **가장 작은 주소(Least Address)**에 저장됨

📌 **정리하면:**

- 메모리는 **바이트 주소로 관리**되며,
- **4바이트 정렬**을 유지해야 하며,
- **MIPS는 빅 엔디언(Big Endian) 방식**을 따른다.

---
### **Littele vs Big endian**

Example
- vaiable x has 4-byte value of 0x01234567
- Address given by &x is 0x100

![](../images/Pasted%20image%2020250319102729.png)


- **Big Endian**: 주소가 작은 곳부터 **가장 큰 값**이 저장되고, 점점 작은 값이 뒤에 배치된다.
- **Little Endian**: 주소가 작은 곳부터 **가장 작은 값**이 저장되고, 점점 큰 값이 뒤에 배치된다.


---
### **Memory Operand Example 1**

#### C Code

```c
g = h + A[8];
```

- `g` → `$s1`
- `h` → `$s2`
- `A` 배열의 **베이스 주소** → `$s3`

#### Compiled MIPS Code

```assembly

# 배열 A의 인덱스 8은 오프셋 32를 필요로 함 
# (4바이트 * 8 = 32) 
lw $t0, 32($s3)   # A[8]의 값을 로드하여 $t0에 저장 
add $s1, $s2, $t0 # g = h + A[8]

```

- 배열은 **4바이트(Word) 단위**로 저장되므로,  
- 인덱스 8에 해당하는 메모리 오프셋은 `8 × 4 = 32` 바이트가 됨.
- `lw` 명령어를 사용해 `A[8]`의 값을 `$t0`에 로드한 후,
- `add` 명령어로 `h`와 `A[8]`을 더해 `g`에 저장.

---
### **Memory Operand Example2**

#### C Code

```c
A[12] = h + A[8]
```

- `h` → `$s2`
- `A` 배열의 **베이스 주소** → `$s3`

#### Compiled MIPS Code

```assembly

# 배열 A의 인덱스 8은 오프셋 32를 필요로 함 
# (4바이트 * 8 = 32) 

lw $t0, 32($s3)   # A[8]의 값을 로드하여 $t0에 저장 
add $$t0, $s2, $t0
sw $t0, 48($s3)

```

- 배열은 **4바이트(Word) 단위**로 저장되므로,  
- 인덱스 8에 해당하는 메모리 오프셋은 `8 × 4 = 32` 바이트가 됨.
- `lw` 명령어를 사용해 `A[8]`의 값을 `$t0`에 로드한 후,
- `add` 명령어로 `h`와 `A[8]`을 더해 `$t0`에 저장.
- 인덱스 12에 해당하는 메모리 오프셋은 `12 × 4 = 48` 바이트가 됨.
- `sw` 명령어를 사용해 `A[12]`에 `$t0`를 저장.


---
### **Registers vs. Memory**

- **레지스터(Register)**는 **메모리보다 더 빠르게** 접근할 수 있음.
- 메모리에서 데이터를 가져오거나 저장하는 작업은 **`load`와 `store` 명령어**가 필요하며, 이는 **더 많은 명령어 실행**을 요구함.

#### **컴파일러의 역할**

컴파일러는 가능한 한 **많은 변수**를 **레지스터**에 할당해야 함.

- **자주 사용되지 않는 변수**만 메모리에 저장 (spill)
- **레지스터 최적화**가 성능에 중요한 영향을 미침

---
### **Immediate Operands**

- 상수(Constant) 데이터를 **명령어 내부에서 직접 지정**할 수 있음.
    ```assembly
    addi $s3, $s3, 4  # $s3에 4를 더함
    ```
    

#### Subtraction with Immediate Values

MIPS에는 **subtract immediate(`subi`) 명령어가 없음**.  
대신 **음수 상수(negative constant)**를 사용하여 뺄셈을 수행함.

```assembly
addi $s2, $s1, -1  # $s2 = $s1 - 1
```

 
> **📌 Design Principle 3: ** 보통의 경우에 더 빠르게 하라
- 작은 상수(Small Constants)는 **자주 사용됨**.
- **Immediate Operand**를 사용하면 **추가적인 load 명령어 없이** 빠르게 연산 가능.

---
### **The Constant Zero**

- MIPS의 **레지스터 0(`$zero`)**는 항상 **상수 0**을 저장하고 있음.
- 값을 변경할 수 없음 (**cannot be overwritten**).
- 일반적인 연산에서 유용하게 활용됨.

---
### **Unsigned Binary Integers**

**n비트의 부호 없는 이진 정수(Unsigned Binary Integer)**는 다음과 같은 식으로 표현됨:

$$x=xn−1⋅2n−1+xn−2⋅2n−2+⋯+x1⋅21+x0⋅20x = x_{n-1} \cdot 2^{n-1} + x_{n-2} \cdot 2^{n-2} + \dots + x_1 \cdot 2^1 + x_0 \cdot 2^0$$

모든 비트가 **크기를 나타내는 값**으로 사용되며, **부호 비트(Sign Bit)가 없음**.

---

### **범위 계산**

$$0≤x≤2n−10 \leq x \leq 2^n - 1$$

- 4비트: `0` ~ `15`
- 8비트: `0` ~ `255`
- 16비트: `0` ~ `65,535`
- 32비트: `0` ~ `4,294,967,295`

---
### **Signed Binary Integers**

부호 있는 정수(Signed Integer)는 **이진수 표현**에서 가장 높은 비트(MSB, Most Significant Bit)를 **부호 비트(Sign Bit)**로 사용하여 음수를 표현할 수 있음.

#### **2의 보수(Two’s Complement) 표현**

n비트의 정수 `x`는 다음과 같이 나타냄:

$$x=−xn−1⋅2n−1+xn−2⋅2n−2+⋯+x1⋅21+x0⋅20x = -x_{n-1} \cdot 2^{n-1} + x_{n-2} \cdot 2^{n-2} + \dots + x_1 \cdot 2^1 + x_0 \cdot 2^0$$


- `x_{n-1}`: **부호 비트(Sign Bit)**
    - `0`: 양수
    - `1`: 음수
- 나머지 비트는 **크기를 나타내는 값**

#### **예제: 4비트 Signed Integer**

| 2진수    | 10진수 해석                                                                                  | 값    |
| ------ | ---------------------------------------------------------------------------------------- | ---- |
| `0111` | $$ 0×(−23)+1×22+1×21+1×200 \times (-2^3) + 1 \times 2^2 + 1 \times 2^1 + 1 \times 2^0 $$ | `+7` |
| `0001` | $$0×(−23)+0×22+0×21+1×200 \times (-2^3) + 0 \times 2^2 + 0 \times 2^1 + 1 \times 2^0$$   | `+1` |
| `0000` | $$0×(−23)+0×22+0×21+0×200 \times (-2^3) + 0 \times 2^2 + 0 \times 2^1 + 0 \times 2^0$$   | `0`  |
| `1111` | $$1×(−23)+1×22+1×21+1×201 \times (-2^3) + 1 \times 2^2 + 1 \times 2^1 + 1 \times 2^0$$   | `-1` |
| `1000` | $$1×(−23)+0×22+0×21+0×201 \times (-2^3) + 0 \times 2^2 + 0 \times 2^1 + 0 \times 2^0$$   | `-8` |

#### Specific numbers
- 0 : 0000 0000 ... 0000
- -1 : 1111 1111 ... 1111
- Most-negative : 1000 0000 ... 0000
- Most-positive : 0111 1111 ... 1111

---
### **Signed Negation**

#### 2의 보수(Two’s Complement) 방식으로 음수 표현

1. **1의 보수(Complement)**: 모든 비트를 반전 (1 → 0, 0 → 1)
2. **1을 더하기(Add 1)**

#### **수식**

x+x‾=1111...11112=−1x + \overline{x} = 1111...1111_2 = -1 x‾+1=−x\overline{x} + 1 = -x

#### **예제: +2를 음수로 변환**

1. **+2의 2진 표현:**
    
    ```
    0000 0000 ... 0010₂
    ```
    
2. **1의 보수 적용:**
    
    ```
    1111 1111 ... 1101₂
    ```
    
3. **1을 더함:**
    
    ```
    1111 1111 ... 1110₂
    ```
    
    → `-2`의 2의 보수 표현

---
### **Sign Extension**

#### **정수를 더 많은 비트로 표현**

- 숫자의 값을 유지하면서 비트 수를 늘리는 방법
- **부호(Sign Bit)를 유지해야 함**

#### **MIPS 명령어에서 사용되는 경우**

- `addi`: 즉시 값(Immediate Value) 확장
- `lb`, `lh`: 바이트(Byte) 또는 하프워드(Halfword) 로드 시 확장
- `beq`, `bne`: 분기(Branch) 시 변위(Displacement) 확장

#### **부호 확장의 원칙**

- **부호 비트를 왼쪽으로 복제**
- **부호 없는 값(Unsigned Value)**의 경우 **0을 추가**

#### **예제: 8비트를 16비트로 확장**

1. **양수 (+2)의 확장**
    
    ```
    0000 0010  →  0000 0000 0000 0010
    ```
    
2. **음수 (-2)의 확장**
    
    ```
    1111 1110  →  1111 1111 1111 1110
    ```
    

부호 확장을 통해 **음수는 그대로 음수로 유지되고, 양수는 값이 보존됨**.

---
## **Representing Instructions**

- 명령어는 **이진수(binary)** 로 인코딩된다.
- 이를 **머신 코드(machine code)** 라고 부른다.
- MIPS 명령어는 **32비트 길이의 명령어 워드(word)** 로 인코딩된다.
- 소수의 형식을 사용하여 연산 코드(opcode), 레지스터 번호 등을 표현한다.
- 포맷이 규칙적이어서 해석하기 쉽다.

### 🗂️ 레지스터 번호

| 이름          | 번호 범위 |
| ------------- | --------- |
| `$t0` ~ `$t7` | 8 ~ 15    |
| `$t8` ~ `$t9` | 24 ~ 25   |
| `$s0` ~ `$s7` | 16 ~ 23   |

---
## **MIPS R-format instructions**

![](../images/Pasted%20image%2020250326132437.png)


## Instuction fields

- **op** : 연산 코드 (operation code)
- **rs** : 첫 번째 소스 레지스터 번호 (first source register number)
- **rt** : 두 번째 소스 레지스터 번호 (second source register number)
- **rd** : 결과를 저장할 목적지 레지스터 번호 (destination register number)
- **shamt** : 쉬프트 양 (shift amount) → 현재는 `00000` 고정
- **funct** : 함수 코드 (function code) → opcode를 보완하여 연산을 구체화함

`opcode` 와 `funct` 를 함께 사용하여 어떤 명령어인지 구체적으로 표현한다.
레지스터는 총 **32개** 존재하며, 각각을 **5비트**로 표현한다.

## R-format Example

```assembly
add $t0, $s1, $s2
```

![](../images/Pasted%20image%2020250326132948.png)


`$s1` 레지스터와 `$s2` 레지스터의 값을 더해서 **$t0**에 저장하라는 의미

```
00000010001100100100000000100000
```

어셈블리어에서는 명령어를 `rd, rs, rt` 순서로 작성한다. 하지만 R-format으로 인코딩될 때는 레지스터가 `rs, rt, rd` 순서로 배치된다. 

---
# **MIPS I-format Instructions**

![](../images/Pasted%20image%2020250326133525.png)


## Imediate arithmetic and load/store instructions

- `rs`: 연산에 사용되는 **소스 레지스터**
- `rt`: 상황에 따라 **소스 또는 목적지 레지스터**로 사용됨
- `constant`: 16비트 상수값, 범위는 **-2¹⁵ ~ 2¹⁵ - 1**
- `address`: 메모리 접근 시 사용하는 **주소 오프셋(offset)**

![](../images/Pasted%20image%2020250326133823.png)



#### ✅ `lw` (load word)

- **형태**: `lw rt, offset(rs)`
- **기능**: `rs`가 가리키는 메모리 주소에서 `offset`만큼 떨어진 곳에 있는 **값을 읽어**와서 `rt`에 저장
- **역할**: 메모리 → 레지스터
    
```assembly
lw $t0, 4($s1)
```
→ `$s1 + 4` 주소에 있는 값을 `$t0`에 로드
→ 여기서 rt 목적지의 역할을 한다.

#### ✅ `sw` (store word)

- **형태**: `sw rt, offset(rs)`
- **기능**: `rt`에 있는 **값을 메모리에 저장**  / 주소는 `rs + offset`으로 계산
- **역할**: 레지스터 → 메모리

```assembly
sw $t0, 4($s1)
```

→ `$t0`의 값을 `$s1 + 4` 주소에 저장
→ 여기서 rt 는 source의 역할을 한다. 

#### ✅ `addi` (add immediate)

- **형태**: `addi rt, rs, immediate`
- **기능**: `rs`의 값과 `immediate` 값을 더해서 결과를 `rt`에 저장
- **역할**: 간단한 정수 덧셈

```assembly
addi $t0, $s1, 10
```

→ `$s1 + 10` 결과를 `$t0`에 저장


---
# **Stored Program Computers**

<div style="display: flex; align-items: flex-start;">
  <div style="margin-right: 20px;">
    <h3>The BIG Picture</h3>
    명령어는 <strong>데이터처럼 이진수(binary)</strong>로 표현된다.  
    <ul>
      <li>명령어와 데이터는 <strong>모두 메모리에 저장</strong>된다.</li>
      <li>프로그램들은 <strong>다른 프로그램(컴파일러, 링커 등)</strong> 안에서 동작한다.</li>
      <li><strong>바이너리 호환성(binary compatibility)</strong> 덕분에, 컴파일된 프로그램은 <strong>다른 컴퓨터에서도 실행 가능</strong>하다.</li>
    </ul>
  </div>
  <img src="Pasted image 20250326140149.png" alt="The BIG Picture" width="250">
</div>


---
# **Memory Layout**

- **Text**: 프로그램 코드가 저장되는 영역
- **Static data**: 전역 변수(static/global variables)가 저장되는 영역
- **Dynamic data**: 동적 메모리(Heap)가 저장되는 영역 (`malloc` 등으로 할당됨)
- **Stack**: 함수 호출 시 사용되는 자동 저장 공간 (지역 변수, 리턴 주소 등)

![](../images/Pasted%20image%2020250326141623.png)



---
# **Logical Operations**

![](../images/Pasted%20image%2020250326141813.png)



## **Shift Operations**

![](../images/Pasted%20image%2020250326142437.png)


#### ✅ Shift Left Logical (`sll`)

- **op = 0**, **funct = 0**
- 왼쪽으로 i비트 쉬프트하고, **빈 자리는 0으로 채움**
- `sll` by `i` bits → **2ⁱ 배 곱하기**
- **사용 예시**: 곱셈을 빠르게 수행할 때

#### ✅ Shift Right Logical (`srl`)

- **op = 0**, **funct = 2**
- 오른쪽으로 i비트 쉬프트하고, **빈 자리는 0으로 채움**
- `srl` by `i` bits → **2ⁱ로 나누기 (정수 나눗셈)**
- **unsigned 정수 전용**

![](../images/Pasted%20image%2020250326142802.png)


```assembly
sll $t2, $s0, 4   # $t2 = $s0 << 4  (왼쪽으로 4비트 shift)
srl $t2, $s0, 4   # $t2 = $s0 >> 4  (오른쪽으로 4비트 shift)
```

💡 의미:
`sll`: `$s0` 값을 왼쪽으로 4비트 이동, 빈 자리는 0으로 채움 -> 결과를 `$t2`에 저장
`srl`: `$s0` 값을 오른쪽으로 4비트 이동, 빈 자리는 0으로 채움 -> 결과를 `$t2`에 저장

## **AND Operations**

- **AND 연산은 특정 비트만 선택(select)** 하고, 나머지 비트는 **0으로 초기화(clear)** 하는 데 유용함
- 주로 **마스킹(masking)** 작업에 사용됨
- 한 워드(word)에서 원하는 비트만 남기고, **다른 비트는 모두 0으로 만든다**

![](../images/Pasted%20image%2020250326143135.png)


- `$t1`과 `$t2`를 비트 단위로 **AND 연산**해서 결과를 `$t0`에 저장
- 각 비트 위치에서 둘 다 1일 때만 1이 됨, 나머지는 0


## **OR Operations** 

- 특정 비트를 **1로 설정(set)** 할 때 유용함
- 원하는 비트만 1로 만들고, **나머지 비트는 그대로 유지**
- 워드(word) 안에서 **특정 비트를 포함(include)** 시키는 데 사용됨

![](../images/Pasted%20image%2020250326143455.png)


- `$t1`과 `$t2`를 **비트 단위 OR 연산**해서 결과를 `$t0`에 저장
- **하나라도 1인 자리**는 결과도 1이 됨


## **NOT Operations**

- 비트를 **반전(invert)** 시킬 때 사용됨
- **0 → 1**, **1 → 0** 으로 바뀜
- 워드(word) 내의 모든 비트를 뒤집는 데 유용함
- MIPS에는 단독 `NOT` 명령어가 없음 대신, **`NOR` 명령어**를 이용해 `NOT` 연산을 구현함 
- `nor`은 3-operand 연산으로 동작함
	- `a nor b  ≡  not (a OR b)`
	→ 즉, `a`와 `b`를 OR 한 다음, **그 결과를 NOT** 하는 것

![](../images/Pasted%20image%2020250326143830.png)


---
# **Conditional Operations**

```assembly
beq rs, rt, L1
```

- `rs`와 `rt`가 **같으면** → 레이블 `L1`으로 분기
- **조건 분기 (equal)**
- **I-format** 사용

```assembly
bne rs, rt, L1
```

- `rs`와 `rt`가 **다르면** → 레이블 `L1`으로 분기
- **조건 분기 (not equal)**
- **I-format** 사용

```assembly
j L1
```

- 조건 없이 **무조건적으로** 레이블 `L1`로 점프
- **J-format** 사용

![](../images/Pasted%20image%2020250326144439.png)


---
# **Compiling If Statements**

```C
if ( i == j ) f = g + h;
else f = g - h;

f, g, ... in `$s0`, `$s1`, ...

```


```assembly
bne $s3, $s4, Else     # if (i != j) -> else로 점프
add $s0, $s1, $s2      # f = g + h
j Exit                 # if 분기 이후 exit으로 점프
Else: sub $s0, $s1, $s2 # f = g - h
Exit: ...              # 이후
```

만약 `j Exit` 가 없다면 `i` 와 `j` 가 같지 않더라도 내려오다 보면 Else로 가기 때문에, 같다면 바로 Exit으로 이동하도록 한다!

![](../images/Pasted%20image%2020250326144755.png)


---
# **Compiling Loop Statements**

```c
while (save[i]==k) i += 1;
i in $s3, k in $s5, address of save in $s6
```

```assembly
Loop:
	sll $t1, $s3, 2        # $t1 = i * 4 (워드 단위 주소 계산)
	add $t1, $t1, $s6      # $t1 = save + i*4 → save[i]의 실제 주소
	lw  $t0, 0($t1)        # $t0 = save[i]
	bne $t0, $s5, Exit     # if (save[i] != k) -> Exit
	addi $s3, $s3, 1       # i = i + 1
	j Loop                 # 반복
Exit: ...
```

### `sll $t1, $s3, 2  ` 
`$s3`의 값을 2비트 왼쪽으로 시프트 한다는건 2^2 = 4배를 해준다는 의미이다.

#### 왜 2비트를 시프트 했는데 4배가 될까?

2진수의 구조를 보면서 설명하면, 예를 들어 2진수 `00000101`은 **10진수로 5**다.

```
00000101  → 2³ × 0 + 2² × 1 + 2¹ × 0 + 2⁰ × 1 = 4 + 1 = 5
```

왼쪽으로 1비트 시프하면 아래와 같다.

```
00000101  → 00001010 → 2³ × 1 + 2² × 0 + 2¹ × 1 + 2⁰ × 0 = 8 + 2 = 10
```

두 개를 비교해보면 비트를 1 왼쪽으로 시프트 하면 모든 자리의 2의 지수가 1씩 커진다. 즉 전체 숫자의 2배가 된다!! **왼쪽으로 n번 시프트하면 전체 숫자는 2ⁿ배가 된다.**

다시 돌아가서 
`sll $t1, $s3, 2` 는 `$s3`의 값(`i`)을 2비트 왼쪽으로 시프트해서, 
`i × 4`의 값을 `$t1`에 저장하는 명령어이다.

이유는 save 배열이 `int`형이기 때문에, 
각 인덱스가 차지하는 크기가 4바이트(1워드)이기 때문이다.

즉, 배열의 시작 주소에 i × 4 만큼 더해주면 
save[i]의 정확한 메모리 주소에 접근할 수 있다.


#### `add $t1, $t1, $s6`

`$s6`에 있는 것은 save 배열의 시작 주소이기 때문에 `$t1` 의 값을 시작 주소에 더해줘 진짜 배열의 인덱스에 접근한거지

#### `lw  $t0, 0($t1)`
 
근데 `$t1`에 있는건 그냥 save 배열의 주소이기 때문에, `lw` 를 사용해서 `$t1` 의 데이터를 `$t0` 로 불러와야 한다. 이거 보면 레지스터에는 데이터가 담겨도 되고, 주소도 담겨도 되는거 같음! 주소도 데이터라 할 수 있으니깐

#### `bne $t0, $s5, Exit` 

이제 $t0 에 담긴 값과 $s5에 담긴 값을 비교해서 다르면 Exit으로 가고, 같다면 아래로 내려간다.

#### `addi $s3, $s3, 1`

`$s3` 에 1을 더해준다! `$s3`가 나타내는 것은 `i`이기 때문에 1을 더해줘서 다음 index로 가려는거지

#### 근데 `add`와 `addi`의 차이점은 뭘까?

> `add`는 **레지스터 + 레지스터**,  
> `addi`는 **레지스터 + 상수(immediate)**

지금은 이렇게만 알아두면 될 듯 하다!


---
# **Basic Blocks**

- **Basic block**은 **연속된 명령어들의 묶음**으로, **중간에 분기(branch)** 가 없고 (※ 끝에는 있을 수 있음) **외부에서 진입하는 분기 타겟**도 없음 (※ 시작점은 예외)

- **분기 없는 직선 코드 흐름**
- **시작~끝까지 순차적으로 실행됨**

- **컴파일러 최적화 단위**로 사용됨
- 고급 프로세서는 **basic block 단위로 실행 속도 향상 가능**

![](../images/Pasted%20image%2020250326160151.png)



---

# **More Conditional Operations**

상태가 `true`면 결과 값은 1이고, `false`면 0이다.

```assembly
slt rd, rs, rt
```

rs < rt 일 때, rd에 1을 할당, 그렇지 않다면 0을 할당

```assembly
slti rt, rs, constant
```

rs < constant 일 때, rt에 1을 할당, 그렇지 않다면 0을 할당

#### `beq`, `bne`의 조합으로 사용할 수 있다.

```assembly
slt $t0, $s1, $s2 # if ($s1 < $s2)
bne $t0, $zero, L # branch to L
```

---
# **Branch Instruction Design**

#### ❓ 왜 MIPS에는 `blt`, `bge` 같은 명령어가 없을까?

- `<`, `≥` 같은 비교 연산은 **`=`나 `≠`보다 하드웨어 구현이 느림**
- 이런 복잡한 비교를 분기(branch)와 **한 명령어에 합치면**,  명령어 하나 처리하는 데 **더 많은 회로와 시간**이 필요함
- 결국 **모든 명령어의 클럭 속도까지 느려짐** (전반적인 성능 저하)

#### ✅ 그래서 어떻게 했나?

- **`beq`, `bne` 같은 단순 비교만 하드웨어에 포함**
- `<`, `>`, `<=`, `>=` 같은 복잡한 비교는  → **별도의 `slt` 같은 명령어로 분리해서 처리**

---
# **Signed vs Unsigned**

Signed comparison : `slt`, `slti`
Unsigned comparison : `sltu`, `sltui`

![](../images/Pasted%20image%2020250326161918.png)


**같은 비트 값이라도 signed/unsigned 해석에 따라 완전히 다르게 비교된다.**  
비트 자체는 그냥 저장된 **0과 1의 나열**일 뿐이고,  
그것을 **어떻게 해석할지는 명령어(slt vs sltu)나 프로그래머의 관점**에 따라 달라진다.


---
## **Proceduer Calling**


1. **파라미터를 레지스터에 저장**  
    → 함수에 전달할 인자(argument)를 지정된 레지스터에 넣는다
    
2. **함수로 제어를 이동**  
    → `jal` 같은 명령어로 함수 호출
    
3. **함수를 위한 스토리지 확보**  
    → 스택 프레임(stack frame) 등으로 지역 변수 공간 확보
    
4. **함수 본연의 작업 수행**  
    → 함수 내부 로직 실행
    
5. **결과 값을 레지스터에 저장**  
    → 반환값을 호출자(caller)를 위한 레지스터에 넣음 (보통 `$v0`)
    
6. **호출 위치로 복귀**  
    → `jr $ra` 명령어로 복귀 주소(`$ra`)로 점프

---
## **Register Usage**

|레지스터|용도|번호|비고|
|---|---|---|---|
|`$a0`–`$a3`|인자(Arguments)|`$4`–`$7`|함수에 전달할 파라미터|
|`$v0`, `$v1`|반환값(Result values)|`$2`, `$3`|함수 결과|
|`$t0`–`$t9`|임시값(Temporaries)|`$8`–`$15`, `$24`, `$25`|**callee가 덮어써도 됨**|
|`$s0`–`$s7`|저장용(Saved)|`$16`–`$23`|**callee가 저장/복구해야 함**|
|`$gp`|전역 포인터(Global Pointer)|`$28`|static 데이터 접근용|
|`$sp`|스택 포인터(Stack Pointer)|`$29`|스택 최상단 위치|
|`$fp`|프레임 포인터(Frame Pointer)|`$30`|함수의 스택 프레임 기준점|
|`$ra`|리턴 주소(Return Address)|`$31`|호출한 함수로 돌아갈 주소 저장|

---

## **Procedure Call Instructons**

#### `jal ProcedureLabel` _(J-format)

- 현재 명령어 다음 주소 (`PC + 4`)를 `$ra`에 저장
- 지정된 함수 주소로 점프
    

#### `jr $ra` _(R-format)_
    
- `$ra`에 저장된 주소를 복사하여 program counter에 복사 점프 → 호출한 곳으로 복귀
- 컴퓨티드 점프(computed jump)에도 사용 가능  (예: `switch-case` 구문 구현)

---
## **Leaf Procedure Example**

```C++
int leaf_example ( int g, h, i, j)
{
	int f;
	f = ( g + h ) - ( i + j );
	return f;
}
```

| 변수       | 레지스터  |
| -------- | ----- |
| `g`      | `$a0` |
| `h`      | `$a1` |
| `i`      | `$a2` |
| `j`      | `$a3` |
| `f`      | `$s0` |
| `return` | `$v0` |

![](../images/Pasted%20image%2020250330181714.png)

```assemby
addi $sp, $sp, -4
```

`$sp` 는 스택 포인터인데 스택의 가장 아래를 가리킨다.  addi 를 통해 스택 포인터의 위치를 -4 하여 stack에 새로운 공간(4바이트)을 할당한다.

```assembly
sw $s0, 0($sp)
```

`$s0` 의 값, 즉 f를 `$sp` 에 저장한다.  왜 저장하냐면 $s0의 값은 함수 밖에서 사용되고 있었을 수도 있기 때문에 함수가 끝날 경우 복구시켜줘야 한다. 그래서 stack 에 따로 저장해두는 것!

```assembly
add $t0, $a0, $a1
add $t1, $a2, $a3
sub $s0, $t0, $t1
```

함수의 변수를 더하고 임시 변수에 저장하고, 임시 변수들끼리 빼서 스택에 저장한 $s0에 저장한다.

```assembly
add $v0, $s0, $zero
```

저장된 $s0의 값을 결과 값으로 옮긴다.

```assembly
lw $s0, 0($sp)
addi $sp, $sp, 4
```

$sp 의 값을 $s0에 불러온다. 즉 함수가 끝날 때 원래 $s0값을 복구해주는 것이다. 

```assembly
jr $ra
```

**복귀 주소($ra)** 로 점프하여  호출한 함수로 돌아감
`jal`로 함수가 호출됐기 때문에, `$ra`에 복귀 주소가 저장되어 있다.

---
## Non-Leaf Procedures

Non-Leaf Procedures는 다른 함수를 호출하는 함수를 말한다.

중첩 호출이 있는 경우, **caller는 스택에 다음 정보를 저장**해야 함:
    - **return address**
    - 다음 호출에서 필요한 **변수**나 **임시 레지스터 값**
        
함수 호출이 끝난 후에는,  **저장해둔 값들을 스택에서 복구**해야 함


---
## **Non - Leaf Procedure Example**

```C++
int fact (int n)
{ 
	if (n < 1) return 1;
	else return n * fact(n - 1);
}
```

| 변수     | 레지스터  |
| ------ | ----- |
| n      | `$s0` |
| result | `$v0` |


![](../images/Pasted%20image%2020250330183432.png)

- `$ra` RET (org return address)
- `$a0` N (org n)


```assembly
addi $sp, $sp, -8 # adjust stack for 2 items
sw $ra, 4($sp) # save return address
sw $a0, 0($sp) # save argument
```

- `addi $sp, $sp, -8`: 스택에 8바이트 공간 확보 (int 2개 크기)
- `4($sp)`: 스택 포인터 기준 4바이트 위 → `$ra` 저장
- `0($sp)`: 스택 포인터가 가리키는 곳 → `$a0` 저장

```assembly
slti $t0, $a0, 1 # test for n < 1
beq $t0, $zero, L1
```

- `$a0`가 **1보다 작으면** → `$t0 = 1` → `beq` 안 됨 → 아래로 진행 (base case 처리)
- `$a0`가 **1 이상이면** → `$t0 = 0` → `beq $t0, $zero, L1` → **L1로 분기 (재귀 호출)**

```assembly
addi $v0, $zero, 1 # if so, result is 1
addi $sp, $sp, 8 # pop 2 items from stack
jr $ra # and return
```

- `addi $v0, $zero, 1`   반환값 1을 `$v0`에 저장 (base case: `if (n < 1) return 1;`)
- 앞서 저장했던 `$ra`, `$a0`를 스택에서 **복구하지 않고 버리고** → **스택 공간만 해제**
	base case에서는 **값을 다시 사용할 일이 없기 때문에**  **스택 포인터만 원래대로 되돌리고**, 그냥 복귀하면 됨, 재귀가 끝났기 때문에 임시 값들을 복구 안 해도 되는거지!
	
- `jr $ra`  를 통해 함수 호출 전으로 복귀

```assembly
addi $a0, $a0, -1 # else decrement n 
jal fact # recursive call
```

$a0의 값, 즉 n을 -1을 해주고 fact 함수를 다시 실행해준다.

---
##### L1 (재귀 호출 이후 복귀 지점)

```assembly
lw $a0, 0($sp) # restore original n
lw $ra, 4($sp) # and return address
addi $sp, $sp, 8 # pop 2 items from stack
```

- `$a0`에 저장된 원래 n 값 불러오기
- `$ra`에 리턴 주소 복원
- 증가시켜서 스택 포인터 원래 상태로 복구

```assembly
mul $v0, $a0, $v0 # multiply to get result
jr $ra # and return
```

- 현재 n과 재귀에서 돌아온 결과를 곱함
- 최종 결과를 `$v0`에 저장 후 복귀

----
## **Local Data on the Stack**

![](../images/Pasted%20image%2020250330185147.png)

- **지역 변수(local data)** 는 **callee(호출된 함수)** 가 스택에 공간을 할당해 사용
- 이러한 공간은 **프로시저 프레임(Procedure Frame)**  또는 **활성화 레코드(Activation Record)** 라고 불림
- **컴파일러**는 이 구조를 사용해  **함수마다 필요한 스택 공간을 정리하고 관리**함

---
## **Memory Layout**

![](../images/Pasted%20image%2020250330185413.png)

#### ✅ **Text**

- **프로그램 코드**가 저장되는 영역
- 컴파일된 명령어(instructions)들이 이곳에 위치함
    
#### ✅ **Static Data**

- **전역 변수**, **정적 변수**, **상수 배열**, **문자열 상수** 등이 저장됨
- C에서 `static`, `const`로 선언된 데이터가 해당
- `$gp`(global pointer)는 이 영역을 기준으로 offset 접근이 가능하도록 초기화됨

#### ✅ **Dynamic Data (Heap)**

- 런타임 중에 동적으로 할당되는 메모리 영역
- 예: `malloc`, `new` 같은 동적 메모리 할당
- 프로그램 실행 중 크기가 늘어나거나 줄어들 수 있음

#### ✅ **Stack**

- **자동 저장소 (automatic storage)**
- 함수 호출 시 지역 변수, 리턴 주소, 레지스터 저장 등이 이곳에서 처리됨
- 함수 호출 시 아래로(stack 감소), 함수 종료 시 위로(stack 증가)

---
## **Character Data**

#### ✅ ASCII

- **총 128개 문자**
- 95개는 **출력 가능한 문자(graphic)**
- 33개는 **제어 문자(control)**

#### ✅ Latin-1 (ISO 8859-1)

- **총 256개 문자**
- ASCII 포함 + **96개 추가 graphic 문자**

#### ✅ Unicode

- **32비트 문자 집합** (이론상 2³²개 표현 가능)
- 대부분의 **세계 문자 체계 + 기호** 포함
- 사용 예: Java, C++의 wide character (`wchar_t`)

#### ✅ UTF-8 / UTF-16

- **Unicode의 가변 길이 인코딩 방식**
- UTF-8: 1~4바이트
- UTF-16: 2 또는 4바이트

---
## **Byte/Halfword Operations**

- **비트 연산(bitwise operations)** 을 이용해 **문자열 처리 등**에 유용하게 사용됨
- MIPS는 **바이트(byte), 하프워드(halfword)** 단위의 로드/스토어를 지원함
좋아! 주신 내용을 기준으로 **MIPS의 바이트/하프워드 메모리 접근**과 관련된 내용을 깔끔하게 정리해줄게:

| 명령어                  | 설명                                 |
| -------------------- | ---------------------------------- |
| `lb rt, offset(rs)`  | **1바이트 로드** + 부호 확장 (sign-extend)  |
| `lh rt, offset(rs)`  | **2바이트(하프워드) 로드** + 부호 확장          |
| `lbu rt, offset(rs)` | **1바이트 로드** + 0으로 확장 (zero-extend) |
| `lhu rt, offset(rs)` | **2바이트 로드** + 0으로 확장               |

- **sign-extend**: 최상위 비트를 32비트까지 복제 (음수 보존)
- **zero-extend**: 상위 비트를 전부 0으로 채움
    
|명령어|설명|
|---|---|
|`sb rt, offset(rs)`|레지스터의 **하위 1바이트만 저장**|
|`sh rt, offset(rs)`|레지스터의 **하위 2바이트(하프워드) 저장**|

- 상위 바이트는 무시됨
- 전체 워드가 아닌 **부분 저장**에 사용됨
    

---
## **String Copy Example**

```C++
void strcpy (char x[], char y[])
{ 
	int i;
	i = 0;
	while ((x[i]=y[i])!='\0')
		i += 1;
}
```

• Addresses of x, y in $a0, $a1
• i in $s0

![](../images/Pasted%20image%2020250330222458.png)

```assembly
addi $sp, $sp, -4 # adjust stack for 1 item
sw $s0, 0($sp) # save $s0
add $s0, $zero, $zero # i = 0
```

$sp의 공간 확보하고  $sp에 $s0의 값을 저장한다. 그리고 $s0는 0을 넣어준다.  

### L1:

```assembly
add $t1, $s0, $a1 # addr of y[i] in $t1
lbu $t2, 0($t1) # $t2 = y[i]
```

- `$a1`에는 **문자 배열 y의 시작 주소**가 들어 있음
- `$s0`은 **인덱스 i**를 담고 있음
- 둘을 더해서 **y[i]의 주소**를 계산 → `$t1`에 저장
- `lbu` 명령어를 사용해 **1바이트(char)** 값을 `$t2`에 로드  → 부호 없이(zero-extend) 상위 비트는 0으로 채워짐

```assembly
add $t3, $s0, $a0 # addr of x[i] in $t3
sb $t2, 0($t3) # x[i] = y[i]
```

- `add`: `x[i]`에 접근할 주소 계산 (`$t3 = $a0 + i`)
- `sb`: `y[i]` 값을 `x[i]` 위치에 저장 (1바이트만 저장)

```assembly
beq $t2, $zero, L2 # exit loop if y[i] == 0 
```

- `t2`에는 현재 `y[i]`의 값이 들어 있음
- 만약 `y[i] == 0`, 즉 **널 문자('\0')** 를 만나면  → **문자열의 끝**이므로 복사 루프 종료
- `beq` 명령어는 조건이 참일 경우 **`L2` 레이블로 분기**


```assembly
addi $s0, $s0, 1 # i = i + 1
j L1 # next iteration of loop
```

i의 값을 1 더해서 다음 글자로 넘어갈 수 있도록! 마지막으로 L1, 즉 처음으로 돌아가서 다시 수행 계속 반복하다보면 언젠가 글자가 끝나서 0을 만나고, L2로 빠져나가게 된다.


### L2:

```assembly
lw $s0, 0($sp) # restore saved $s0
addi $sp, $sp, 4 # pop 1 item from stack
jr $ra # and return
```

- `$s0`는 루프에서 인덱스로 사용된 값이기 때문에  
    → 원래 값으로 **복구**해줘야 함 (callee-saved 레지스터)
- 스택 정리 후 `jr $ra`로 호출한 위치로 **복귀**

---
## **32-bit Constants

- 대부분의 상수는 **작기 때문에**  → **16비트 immediate**로 충분함
- 하지만 **가끔씩 32비트 상수**가 필요한 경우가 있음  → 이럴 때는 **두 개의 명령어 조합**으로 처리

```assembly
lui $rt, constant
```

- 16비트 상수를 **레지스터의 상위 16비트**에 복사
- **하위 16비트는 0으로 채움**

![](../images/Pasted%20image%2020250331004022.png)

> **32비트 상수는 직접 명령어에 표현되지 않고**,  
> **레지스터에 상수를 채워 넣는 방식으로 간접적으로 표현된다.**


MIPS는 명령어 형식이 제한되어 있어서,  
한 번에 32비트를 표현하지 못하고  
**`lui` + `ori`** 조합으로 레지스터에 값을 만들어내는 식!

---
## **Branch Addressing**

분기 명령어는 다음 정보를 포함함:
    - **Opcode**
    - **두 개의 레지스터** (`rs`, `rt`)
    - **분기 대상 주소(target address)**

 대부분의 분기 대상

- **현재 위치(분기 명령어) 기준으로 가까운 곳**
- 즉, 분기 대상은 보통 **앞쪽(forward)** 또는 **뒤쪽(backward)** 의 코드
- 그래서 **절대 주소가 아니라 상대적(offset)** 으로 지정함

![](../images/Pasted%20image%2020250331004320.png)

- **분기 주소 계산 방식:**

```
Target Address = PC + (offset × 4)
```

- 여기서 `offset`은 명령어 안에 들어 있는 **16비트 값**
- 분기 명령어 실행 시점에는 **PC가 이미 4 증가된 상태**  → 즉, **다음 명령어 주소 기준으로 offset을 더함**

**constant 또는 offset 값은 실제로 뒤에 `00` (2비트)** 가 **생략된 것**  즉, **4의 배수로 정렬된 주소**를 나타낸다.
명령어는 **word 단위(4바이트)** 이기 때문에  **주소도 항상 4로 나누어 떨어져야 함** 하위 2비트는 필요 없으므로 **낭비 줄이고 인코딩 공간 절약**한다.

---
## **Jump Addressing**

`j`, `jal`은 **프로그램 내 어디든지** 점프할 수 있다.  → **text segment 전체 범위**가 대상 가능
**Jump 명령어는 전체 주소를 인코딩하지는 않음**  → 명령어는 32비트지만, 그 중 **26비트**만을 사용해 **주소 일부**만 표현한다. 여기도 마찬가지로 뒤에 00가 생략되어 있다. 

![](../images/Pasted%20image%2020250331005350.png)

#### Pseudo-Direct Jump Addressing

> MIPS에서 `j`, `jal` 명령어가 사용하는 **점프 주소 계산 방식**

#### 왜 "pseudo(의사)"인가?

- 명령어에 들어 있는 주소 필드는 **26비트**뿐
- 실제 메모리 주소는 **32비트**
- 따라서 **명령어만으로는 전체 주소를 직접 표현할 수 없음**
    

→ 그래서 **“직접(direct) jump처럼 보이지만, 실제로는 일부만 직접 인코딩**  나머지 비트는 **현재 PC에서 보충**함


#### 주소 계산 방식

```
Target Address = {PC[31:28], address[25:0], 00}
```

- 상위 4비트: **현재 PC의 [31:28]**
- 중간 26비트: **명령어에 포함된 주소 필드**
- 하위 2비트: **항상 00** (4바이트 정렬 → 곱하기 4)
    
---
## **Target Addressing Example**

![](../images/Pasted%20image%2020250331005454.png)

`bne`는 **주소 80012에 위치**해 있고, `PC`는 이미 4 증가된 상태로 `80016` 를 가리킨다.
`Exit`은 **주소 80024**에 위치해 있고 따라서 offset = `(80024 - 80016) / 4 = 2`

여기서 offset은 **"명령어 개수 기준"** 으로 계산되므로,  바이트가 아니라 **word 단위 (4바이트)** 로 나눠줘야 한다. 즉 **2라는 값이 인코딩된다.**



`j` 명령어는 **(pseudo)direct jump** 방식 사용
- 명령어 안에 **26비트 주소**가 들어 있음  이 주소는 **4바이트 단위이므로 하위 2비트는 생략**
    

#### 실제 Jump 주소 계산:

```
Target Address = {PC[31:28], instr_index << 2}
```


- `PC`는 현재 80024
- `Loop`는 80000 → 상위 4비트인 `PC[31:28]`는 같다.
- 따라서 `j Loop`는 정확히 80000으로 점프 가능

##### 왜 상위 4bit가 같은가?

- MIPS는 **text segment(코드 메모리 영역)** 를  일반적으로 **256MB 크기(2²⁸)** 이하로 제한한다.
- 즉, **점프 대상은 같은 상위 4비트 안에 있다는 전제** 하에 프로그램을 구성한다.
- 그래서 상위 4비트(PC[31:28])는 **변하지 않는다고 가정하고** 점프 명령어를 설계한 것!

---
## **Branching Far Away**

분기 대상이 16비트 offset으로 표현하기엔 너무 멀 경우, 어셈블러가 코드를 자동으로 다시 작성한다.

```assembly
	beq $s0, $s1, L1
	bne $s0, $s1, L2
	j L1
L2: …
```

여기서 `$s0`와 `$s1`이 **동일하면 `L1`으로 이동**한다.  하지만 `L1`이 **너무 먼 주소**일 경우,  `bne`를 사용해 `$s0`와 `$s1`이 **다르면 가까운 `L2`로 분기**하고,  **같은 경우에는 `j` 명령어로 `L1`처럼 먼 주소로 이동**할 수 있도록 구성한다.

---
## **Addressing Mode Summary**

![](../images/Pasted%20image%2020250331011005.png)

---
## **Synchoronization**

두 개의 프로세서가 **같은 메모리 영역을 공유**하고 있는 상황에서,  P1이 데이터를 쓰고 P2가 그것을 읽는 경우,  **동기화(synchronization)** 가 제대로 이루어지지 않으면 **데이터 레이스(data race)** 가 발생할 수 있다.

이 경우, **실행 결과가 접근 순서에 따라 달라지게 되며**,  예측할 수 없는 동작이나 버그의 원인이 된다.

이러한 상황에서는 **하드웨어의 도움이 필요**하다.  즉, **atomic(원자적) read/write 메모리 연산**이 요구된다.

이 연산은 **읽기와 쓰기를 하나의 단위로 처리**하며,  **그 사이에 다른 어떤 접근도 허용되지 않는다.**  
→ 이를 통해 데이터 레이스를 방지할 수 있다.

---
## **Synchronization in MIPS**


#### ✅ `ll rt, offset(rs)`

- **Load Linked**
- 지정된 주소의 값을 읽어 `rt`에 저장
- 동시에 **연결 상태(linked state)** 를 유지
    

#### ✅ `sc rt, offset(rs)`

- **Store Conditional**
- 마지막 `ll` 이후에 **그 메모리 주소가 변경되지 않았다면**, 저장 성공 → `rt`에 **1이 저장됨**    
- 만약 **그 사이 누군가 그 주소에 접근했다면**, 저장 실패→ `rt`에 **0이 저장됨**


```assembly
try:
  add $t0, $zero, $s4    # 교환할 값을 $t0에 복사
  ll  $t1, 0($s1)        # 공유 변수의 현재 값 읽기
  sc  $t0, 0($s1)        # 변경 시도 (성공 시 1, 실패 시 0이 $t0에 저장)
  beq $t0, $zero, try    # 실패했으면 다시 시도
  add $s4, $zero, $t1    # 원래 값은 $s4에 저장
```


---
## **Assembler Pseudo-instructions

대부분의 어셈블리 명령어는 **기계어 명령어와 일대일로 대응**된다.  하지만 **Pseudo-instructions(의사 명령어)** 는  말 그대로 **어셈블러의 상상력이 만들어낸 가상의 명령어**다.

즉, 실제 기계어에는 존재하지 않지만,  
어셈블러가 **여러 개의 진짜 명령어로 변환해서 실행**되도록 처리해준다.

```assembly
move $t0, $t1 → add $t0, $zero, $t1
blt $t0, $t1, L → slt $at, $t0, $t1
bne $at, $zero, L
```

`move $t0, $t1`과 같은 명령어는 **실제 기계어에는 존재하지 않는 pseudo-instruction**이다.  
하지만 어셈블러는 이를 자동으로 다음과 같은 **진짜 명령어**로 변환한다:

`add $t0, $zero, $t1`

즉, `$t1`의 값을 `$t0`에 복사하는 동작을  `$zero + $t1`을 더해서 `$t0`에 저장하는 방식으로 구현한 것.

같은 방식으로 `blt $t0, $t1, L` 역시 기계어에는 존재하지 않지만, 어셈블러는 이를 아래와 같이 바꿔준다:

```assembly
slt $at, $t0, $t1 
bne $at, $zero, L
```

- `$t0 < $t1`이면 `$at = 1`
- 그 후 `$at ≠ 0`이면 `L`로 분기

어셈블러가 pseudo-instruction을 **기계어로 번역할 때**,  **필요한 임시 레지스터**는 **`$at`(assembler temporary)** 를 사용하는 것으로 **규약되어 있다.**

---
## **Producing an Object Module**

어셈블러 또는 컴파일러는 **프로그램을 기계어 명령어로 번역**한다.
이때, 프로그램은 **작은 조각(object module)** 단위로 생성되며,  실행에 필요한 다양한 정보를 함께 포함한다. 

|항목|설명|
|---|---|
|**Header**|오브젝트 모듈의 전체 내용을 설명|
|**Text Segment**|번역된 기계어 명령어 (실행 코드)|
|**Static Data Segment**|프로그램 전체 실행 동안 유지되는 전역/정적 데이터|
|**Relocation Info**|절대 주소에 따라 변경되어야 하는 위치 정보 (로딩 시 주소 재조정)|
|**Symbol Table**|전역 정의(global definitions)와 외부 참조(external references) 정보|
|**Debug Info**|기계어와 소스 코드를 연결하기 위한 디버깅 정보|

---
## **Linking Object Module**

링커는 **실행 가능한 이미지(executable image)** 를 생성하는 과정에서 다음 작업들을 수행한다:

1. **세그먼트 병합 (merge segments)**
   여러 오브젝트 파일의 **text, data** 세그먼트를 하나로 통합
        
2. **레이블 해결 (resolve labels)**
   각 심볼(label)의 **실제 주소 결정**
        
3. **주소 의존성 처리 (patch location-dependent refs)**
   명령어나 데이터 내의 **주소 기반 참조**를 실제 메모리 주소로 대체
        
4. **외부 참조 연결 (resolve external references)**
   다른 파일에서 정의된 심볼을 **정확한 위치로 연결**

예전에는 **재배치 가능 로더(relocating loader)** 가 실행 시점에 위치를 조정해야 했지만,
**가상 메모리** 환경에서는  프로그램을 **가상 주소 공간 내의 절대 위치에 바로 배치**할 수 있으므로  
복잡한 재배치 과정 없이도 실행 가능

---
## **Loading a Program**

- **헤더를 읽어 세그먼트 크기 결정**  
    → 실행 파일의 헤더 정보를 통해 `text`, `data`, `bss` 세그먼트의 크기를 파악
    
- **가상 주소 공간 생성**  
    → 프로그램이 실행될 **가상 메모리 공간**을 구성함
    
- **코드 및 데이터 로딩**
    
    - `text` 섹션을 메모리로 복사
    - 데이터 세그먼트를 메모리에 초기화
    - 또는 **페이지 테이블 항목만 설정**해, 나중에 page fault로 불러올 수 있게 함 (lazy loading)
        
- **스택에 인자 설정**  → 프로그램 실행 시 필요한 인자(`argc`, `argv`)들을 스택에 올림
    
- **레지스터 초기화**
    
    - `$sp`: Stack Pointer
    - `$fp`: Frame Pointer        
    - `$gp`: Global Pointer  → 적절한 초기값으로 설정
        
- **시작 루틴(startup routine)으로 점프**  → 인자를 `$a0` 등에 넣고 `main()` 함수를 호출
    
- **`main()` 함수가 종료되면 `exit` 시스템 호출** 수행  → 운영체제에 제어권 반환

---
## **Starting Java Applications

![](../images/Pasted%20image%2020250402101220.png)

C/C++은 **컴파일 시 실행 파일이 직접 기계어로 번역**되기 때문에,  
실행 시에는 **바로 CPU가 실행할 수 있는 형태**가 된다.

반면 **Java는 실행 파일이 바이트코드(bytecode)** 형태로 생성되며,  
이는 **Java Virtual Machine(JVM)** 위에서 동작한다.

JVM은 바이트코드를 **실행 시점에 기계어로 해석 또는 JIT(Just-In-Time) 컴파일**을 통해 변환한다.  
한 번 번역된 코드는 **캐시에 저장**되어 이후 실행 시 **더 빠르게 실행**될 수 있다.

---
## **C sort Example**

C 버블 정렬을 assembly 명령어로 나타내볼게

```C++
void swap(int v[], int k)
{
	int temp;
	temp = v[k];
	v[k] = v[k+1];
	v[k+1] = temp;
}
```

• v in $a0, k in $a1, temp in $t0

![](../images/Pasted%20image%2020250402102611.png)

```assembly
sll $t1, $a1, 2 # $t1 = k * 4
add $t1, $a0, $t1 # $t1 = v+(k*4) # (address of v[k])
```

`sll`을 통해 `k * 4`로 바이트 offset을 구하고 `$t1`에 넣어준다. `$a0`에는 배열의 첫 시작 주소가 들어가 있고, `$t1`을 더해서 `$t1`에 v[k] 의 주소를 넣어준다. 

```assembly
lw $t0, 0($t1) # $t0 (temp) = v[k]
lw $t2, 4($t1) # $t2 = v[k+1]
```

`$t1`의 값을 $t0에 불러오고 $t1으로 부터 4바이트 떨어진 곳 즉 v[k+1]의 값을 $t2에 불러온다.

```assembly
sw $t2, 0($t1) # v[k] = $t2 (v[k+1])
sw $t0, 4($t1) # v[k+1] = $t0 (temp)
```

$t2의 값을 $t1에 저장하고 $t0 의 값을 v[k+1]의 값에 넣어준다.
두 값을 서로 **교환(swap)** 하는 동작 - `v[k] ← v[k+1]`, `v[k+1] ← v[k]`

```assembly
jr $ra
```

저장된 리턴 주소(`$ra`)로 복귀하여  **함수 호출 이후의 코드로 이동**

---
## **The Sort Procedure in C**

```C++
void sort (int v[], int n)
{
	int i, j;
	for (i = 0; i < n; i += 1) {
		for (j = i – 1; j >= 0 && v[j] > v[j + 1]; j -= 1) {
			swap(v,j);
		}
	}
}
```

• v in $a0, n in $a1, i in $s0, j in $s1

다른 함수 호출할 때 `$a` 사용하니까 다른 곳으로 옮겨 놓을 것!
`$s0`, `$s1`도 사용하고 있었을 수 있으니 백업!

![](../images/Pasted%20image%2020250402103647.png)

---
## **The Full Procedure**

![](../images/Pasted%20image%2020250402103752.png)

---
## **Effect of Compiler Optimization**

![](../images/Pasted%20image%2020250402103917.png)

![](../images/Pasted%20image%2020250402103939.png)

**Instruction count**와 **CPI(Cycles Per Instruction)** 는  **단독으로는 정확한 성능 지표가 되기 어렵다.**
**컴파일러 최적화**는 **알고리즘에 따라 성능 차이가 크다.**
**Java**의 경우, **JIT(Just-In-Time) 컴파일된 코드**는 단순 JVM 해석 방식보다 훨씬 빠르며,  경우에 따라 **최적화된 C 코드와 비슷한 성능**을 보이기도 한다.

---
## **Arrays vs Pointer**

- **배열 인덱싱(array indexing)** 은  **인덱스 × 요소 크기**를 계산한 후  배열의 시작 주소(base address)** 에 더하여  해당 요소의 주소를 구함
    
- 반면, **포인터(pointer)** 는  **메모리 주소를 직접 참조**하므로  이러한 **인덱싱 연산 과정을 생략**할 수 있음

![](../images/Pasted%20image%2020250402104855.png)

왼쪽은 배열이고 오른쪽은 포인터를 사용한 코드이다. 어셈블리어로 된 코드를 보면 딱 봐도 pointer를 이용한 코드의 반복되는 부분이 더 짧은 것을 알 수 있다.

C++ 언어에서 포인터 p = p + 1 이라는 말은 주소가 1증가 된 것이 아니라 p가 int 포인터이기 때문에 자동으로 4byte 이동했다고 치환해준다.